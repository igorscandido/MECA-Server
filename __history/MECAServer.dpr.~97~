program MECAServer;

{$APPTYPE CONSOLE}

{$R *.res}

uses
  System.SysUtils,
  Horse,
  Provider.Connection in 'Providers\Provider.Connection.pas' {ProviderConnection: TDataModule},
  Provider.Utils in 'Providers\Provider.Utils.pas',
  Service.Module in 'Services\Service.Module.pas' {ServiceModule: TDataModule},
  Service.Produto in 'Services\Service.Produto.pas' {ServiceProduto: TDataModule},
  System.JSON,
  Dataset.Serialize, // Help works with conversion from Dataset to Json
  Horse.Jhonson,
  Horse.HandleException; // Creates exceptions with status code

var
  DBConnection : TProviderConnection;

  procedure Init;
  begin

    DBConnection := TProviderConnection.Create(nil);

  end;

begin

  Init;

  THorse.Use(Jhonson); // Imports Jhonson module to use JSON in Response
  THorse.Use(HandleException); // Imports Exception Handler for Horse

  {LIST}
  THorse.Get('/produtos',
  procedure(Req : THorseRequest; Res : THorseResponse; Next: TProc)
  var
    LService : TServiceProduto;
    LJson : TJSONArray;
  begin
    LService := TServiceProduto.Create(nil);
    try
      LJson := LService.List.ToJSONArray();
    finally
      LService.Free;
    end;
    Res.Send<TJSONArray>(LJson);
  end);

  {GETBYID}
  THorse.Get('/produtos/:id',
  procedure(Req : THorseRequest; Res : THorseResponse; Next: TProc)
  var
    LService : TServiceProduto;
    LJson : TJSONArray;
    IDProduto : Integer;
  begin
    LService := TServiceProduto.Create(nil);
    try
      IDProduto := Req.Params['id'].ToInt64;
      if LService.GetByID(IDProduto).IsEmpty then
        raise EHorseException.Create(THTTPStatus.NotFound,'O registro não foi encontrado!');

      LJson := LService.qUpdate.ToJSONArray();
    finally
      LService.Free;
    end;
    Res.Send<TJSONArray>(LJson);
  end);

  {INSERT}
  THorse.Post('/produtos',
  procedure(Req : THorseRequest; Res : THorseResponse; Next: TProc)
  var
    LService : TServiceProduto;
    LBody : TJSONObject;
  begin
    LService := TServiceProduto.Create(nil);
    try
      LBody := Req.Body<TJSONObject>;

      if LService.Insert(LBody) then
        Res.Send<TJSONObject>(LService.qUpdate.ToJSONObject).Status(THTTPStatus.Created)
      else
        raise EHorseException.Create(THTTPStatus.NotModified,'Não foi criado');

    finally
      LService.Free;
    end;
  end);

  {UPDATE}
  THorse.Put('/produtos/:id',
  procedure(Req : THorseRequest; Res : THorseResponse; Next: TProc)
  var
    LService : TServiceProduto;
    LBody : TJSONObject;
    LID : Cardinal;
  begin
    LService := TServiceProduto.Create(nil);
    try

      LID := Req.Params['id'].ToInt64;

      if LService.GetByID(LID).IsEmpty then
        raise EHorseException.Create(THTTPStatus.NotFound,'Registro não foi encontrado');

      LBody := Req.Body<TJSONObject>;

      if LService.Update(LBody) then
        Res.Status(THTTPStatus.NoContent)
      else
        raise EHorseException.Create(THTTPStatus.NotModified,'Não foi modificado');

    finally
      LService.Free;
    end;
  end);

  {UPDATE}
  THorse.Delete('/produtos/:id',
  procedure(Req : THorseRequest; Res : THorseResponse; Next: TProc)
  var
    LService : TServiceProduto;
    LID : Cardinal;
  begin
    LService := TServiceProduto.Create(nil);
    try

      LID := Req.Params['id'].ToInt64;

      if LService.GetByID(LID).IsEmpty then
        raise EHorseException.Create(THTTPStatus.NotFound,'Registro não foi encontrado');

      if LService.Delete then
        Res.Status(THTTPStatus.NoContent)
      else
        raise EHorseException.Create(THTTPStatus.NotModified,'Não foi possível deletar!');

    finally
      LService.Free;
    end;
  end);

  THorse.Listen(9000);
end.
